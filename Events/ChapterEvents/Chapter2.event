EventPointerTable(Chapter2EventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, ElodiaDecidesToCharge, 7)
TurnEventPlayer(LuaInAHubFlag, ObjectiveExplanation, 1)
TurnEventPlayer(Ch2ReinforcementFlags, BoatPursuerReinforcements, 1, 255)
TurnEventPlayer(Ch2ReinforcementFlags, IgnacioBoatReinforcements, 3)
TurnEventPlayer(ReadyForTakeoffFlag, TheGangTakesOff, 1, 255)
TurnEventPlayer(WindUpFlag3, WindUpThirdTurn, 1, 255)
TurnEventPlayer(WindUpFlag2, WindUpSecondTurn, 1, 255)
TurnEventPlayer(WindUpFlag1, WindUpFirstTurn, 1, 255)
END_MAIN

BoatPursuerReinforcements:
CHECK_TURNS //checks the turn and returns it
SVAL s1 3 //makes s1 2
SMOD s1 sC s1 // modulates sC by s1 and puts it in s1
BNE 1 s0 s1 // checks if it's an even turn, if not, skip
    SpawnReinforcements(BoatPursuerGriffin)
    SpawnReinforcements(BoatPursuerCavaliers)
LABEL 1
ENUF Ch2ReinforcementFlags
NoFade
ENDA

BoatPursuerGriffin:
UNIT TorchUnit2 GriffinRider Hugo Level(9, Enemy, True) [10,2] 0 0 1 REDA10R4 [IronLance] GoForThroneAI
UNIT

BoatPursuerCavaliers:
UNIT TorchUnit2 SwordCav ElodiaBoss Level(10,Enemy,True) [39,22] 0 0 1 REDA39R22 [IronSword] GoForThroneAI
UNIT TorchUnit2 AxeCav ElodiaBoss Level(10,Enemy,True) [39,24] 0 0 1 REDA39R24 [IronAxe] GoForThroneAI
UNIT

WindUpFirstTurn:
Text(Ch2_3MinutesLeft)
ENUF WindUpFlag2
NoFade
ENDA

WindUpSecondTurn:
Text(Ch2_2MinutesLeft)
ENUF WindUpFlag3
NoFade
ENDA

WindUpThirdTurn:
Text(Ch2_1MinutesLeft)
ENUF ReadyForTakeoffFlag
NoFade
ENDA

ElodiaDecidesToCharge:
CameraCursor(ElodiaBoss)
Text(Ch2ElodiasCharge)
SetAI(ElodiaBoss,0,0,4,0)
SpawnReinforcements(ElodiaWolfSquad)
SpawnReinforcements(GriffinSquad)
Text(Ch2GriffinsShowUp)
ENUT Ch2ReinforcementFlags
NoFade
ENDA

ElodiaWolfSquad:
UNIT TorchUnit Wolfrider Ignacio Level(10,Enemy,True) [39,22] 0 0 1 REDA39R22 [SteelKnife] PursueWithoutHeedAI
UNIT TorchUnit Wolfrider Ignacio Level(10,Enemy,True) [39,24] 0 0 1 REDA39R24 [SteelKnife] PursueWithoutHeedAI
UNIT

GriffinSquad:
UNIT TorchUnit Bombardier Ignacio Level(12,Enemy,True) [36,24] 0 0 1 REDA36R24 [SilverLance, SteelBow] PursueWithoutHeedAI
UNIT TorchUnit GriffinRider Ignacio Level(9,Enemy,True) [37,25] 0 0 1 REDA37R25 [SteelLance] PursueWithoutHeedAI
UNIT TorchUnit GriffinRider Ignacio Level(9,Enemy,True) [35,25] 0 0 1 REDA35R25 [SteelLance] PursueWithoutHeedAI
UNIT 

TheGangTakesOff:
Text(Ch2_0MinutesLeft)
ENUT 3
CALL EndingScene
NoFade
ENDA


ObjectiveExplanation:
CenterTextbox(Chapter2Explanation)
ENUF LuaInAHubFlag
NoFade
ENDA

IgnacioBoatReinforcements:
SpawnReinforcements(IgnacioBoatReinforcementsGroup1)
SpawnReinforcements(IgnacioBoatReinforcementsGroup2)
NoFade
ENDA

IgnacioBoatReinforcementsGroup1:
UNIT Igniswords Vanguard Ignacio Level(8,Enemy,True) [32,16] 0 0 1 REDA33R15 [SteelLance, HandAxe] GroupThreeAI
UNIT Igniswords Archer Ignacio Level(8,Enemy,True) [32,16] 0 0 1 REDA30R15 [SteelBow] GroupThreeAI
UNIT

IgnacioBoatReinforcementsGroup2:
UNIT Igniswords Warden Ignacio Level(8,Enemy,True) [34,5] 0 0 1 REDA34R5 [IronBlade, Javelin] GroupFourAI
UNIT Igniswords Mage Ignacio Level(8,Enemy,True) [30,6] 0 0 1 REDA30R6 [Fire] AttackWhenInRangeAI
UNIT

CharacterBasedEvents:
CharacterEvent(OrlandoHubATalk, GotoChapter1, Lua, Orlando)
CharacterEvent(RahelOrlandoHubATalkFlag, OrlandoRahelTalk, Lua, Orlando)
CharacterEvent(RahelOrlandoHubATalkFlag, OrlandoRahelTalk, Lua, Rahel)
CharacterEvent(CarmenXimoHubATalkFlag, CarmenXimoTalk, Lua, Carmen)
CharacterEvent(CarmenXimoHubATalkFlag, CarmenXimoTalk, Lua, Ximo)
END_MAIN

GotoChapter1:
HubChapterFirstTimeEvent(1, SeenHubA1Pitch, HubA1Pitch)
FancyChapterMapOffset(-1,-1)
FancyChapterPreview(HubA_1, Castle_Hall_Night, Chapter2)
GotoHubChapterIfYes(2, HubA_1, HubA1Accepted)
ENUF OrlandoHubATalk
NoFade
ENDA

OrlandoRahelTalk:
Text(RahelOrlandoBaseSceneText)
NoFade
ENDA

CarmenXimoTalk:
Text(CarmenXimoBaseSceneText)
NoFade
ENDA

LocationBasedEvents:
DurabilityChest(SkillScroll,AcrobatID,31,15)
END_MAIN

MiscBasedEvents:
//actual chapter
AREA EnemyGotOnBoat GameOverMan [19,6] [24,16]
AFEV IgniswordsDead CheckIfIgniswordsAllDead 0

//interlude version
AREA MainRoomToOtherRooms GotoLuasQuarters [7,4] [7,4]
AREA MainRoomToOtherRooms GotoLibrary [0,17] [0,18]
AREA MainRoomToOtherRooms GotoBarracksAndTreasureRoom [5,13] [5,13]

AREA TreasureRoomToMainRoom GoBackToMainRoomFromTreasureRoom [39,23] [39,24]
AREA LuaQuartersToMainRoom GoBackToMainRoomFromLuaQuarters [19,25] [19,25]

AREA WentToPortGreagor GotoPortGreagorFirstTime [1,0] [3,0]
AREA MainRoomToPort GotoPortGreagor [1,0] [3,0]

AFEV Ch2ThisIsAChapter RefreshLua 0
CauseGameOverIfLordDies
END_MAIN

CheckIfIgniswordsAllDead:
CHECK_INAREA Ignacio [30,0] [8, 17]
BNE 1 s0 sC // if true, skip next line
    CHECK_INAREA Igniswords [30,0] [8, 17]
    BNE 1 s0 sC
        Text(Ch2AllIgniswordsDefeated)
        ENUF WindUpFlag1
        GOTO 2
LABEL 1
ENUF IgniswordsDead
LABEL 2
NoFade
ENDA


GameOverMan:
CHECK_ALLEGIANCE (-1)
SVAL s1 2
BNE 1 s1 sC //if it's not 2, it's not an enemy, so we skip
    CALL GameOverStart
LABEL 1
ENUF EnemyGotOnBoat
NoFade
ENDA

GotoLuasQuarters:
InitiateTileChangeInHub(ClearChapter2Traps, AddLuaQuartersTraps, 4, LuaInHerQuarters)
ENUT MainRoomToPort
ENUF LuaQuartersToMainRoom
NoFade
ENDA

AddLuaQuartersTraps:
AddTrap(BlueSigilTrapID, 19, 25)
NoFade
ENDA

ClearLuaQuartersTraps:
RemoveTrapAtCoords(19, 25)
NoFade
ENDA

LuaInHerQuarters:
UNIT Lua Captain 0 Level(10,Ally,False) [19,25] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaBackFromQuarters:
UNIT Lua Captain 0 Level(10,Ally,False) [7,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

GoBackToMainRoomFromLuaQuarters:
InitiateTileRevertInHub(ClearLuaQuartersTraps, AddChapter2Traps, 4, LuaBackFromQuarters, MainRoomSpawnEvent)
ENUF MainRoomToOtherRooms
ENUF MainRoomToPort
NoFade
ENDA

GotoLibrary:
InitiateLoadMapHub(HubALibrary, LuaInDaHubLibrary, LibrarySpawnEvent)
ENUF LibraryToMainRoom
NoFade
ENDA

LuaInDaHubLibrary:
UNIT Lua Captain 0 Level(10,Ally,False) [18,8] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LibrarySpawnEvent:
LoadCutsceneUnits(HubALibraryRazvanGroup)
LoadPlayerUnits(HubALibraryHermenegildoGroup)
TurnUnitGreenIfAlive(Razvan, 1)
TurnUnitGreenIfAlive(Hermenegildo, 2)
NoFade
ENDA

HubALibraryRazvanGroup:
UNIT Razvan 0 0 Level(3,Ally,False) [7,3] 0 0 0 0 [Heal, Torch] NoAI
UNIT

HubALibraryHermenegildoGroup:
UNIT Hermenegildo 0 0 Level(1,Ally,False) [9,3] 0 0 0 0 [IronSword, LightBrand, Vulnerary] NoAI
UNIT

GotoBarracksAndTreasureRoom:
InitiateTileChangeInHub(ClearChapter2Traps, AddTreasureRoomTraps, 1, LuaInDaTreasureRoom, TreasureRoomSpawnEvent)
ENUF TreasureRoomToMainRoom
ENUT MainRoomToPort
NoFade
ENDA

TreasureRoomSpawnEvent:
LoadPlayerUnits(CarmenXimoBaseGroup)
TurnUnitGreenIfAlive(Carmen, 1)
TurnUnitGreenIfAlive(Ximo, 2)
NoFade
ENDA

CarmenXimoBaseGroup:
UNIT Carmen Mage_F 0 Level(1,Ally,False) [28,24] 0 0 0 0 [Fire, Vulnerary] NoAI
UNIT Ximo Archer 0 Level(1,Ally,False) [29,22] 0 0 0 0 [IronBow, Vulnerary] NoAI
UNIT 

GoBackToMainRoomFromTreasureRoom:
InitiateTileRevertInHub(CUSAScrubDuo, ClearTreasureRoomTraps, AddChapter2Traps, 1, LuaOnDaStairs, MainRoomSpawnEvent)
ENUF MainRoomToOtherRooms
ENUF MainRoomToPort
NoFade
ENDA

CUSAScrubDuo:
TurnUnitBlueIfAlive(Carmen, 1)
TurnUnitBlueIfAlive(Ximo, 2)
NoFade
ENDA

MainRoomSpawnEvent:
LoadPlayerUnits(OrlandoRahelUnitGroup)
SpawnQuestSigil(WentToHubA1, 1, RedSigilTrapID, 17, 11)
NoFade
ENDA

OrlandoRahelUnitGroup:
UNIT Orlando 0 0 Level(12,NPC,False) [17,11] 0x0 0x0 0 0 [Bloodletter, Thani] NoAI
UNIT Rahel Civilian_F_2 0 Level(1,NPC,False) [19,10] 0x0 0x0 0 0 [0x0] NoAI
UNIT

GotoPortGreagorFirstTime:
InitiateLoadMapHub(HubA, ClearHubATraps, LuaInDaHubFirstTime, PortGreagorSpawnEvent)
Text(HubABeginningScene3_1)
CamOn
LoadPlayerUnits(BeginningSceneIdaGroup)
Text(HubABeginningScene3_2)
MOVE 15 Ida [0,12]
ENUN
REMOVEUNIT Ida
CamOff
CAMERA_CENTERED Lua
STAL 10
Text(HubABeginningScene3_3)
SetAllHubChapterFlags
ENUT PortToDocks
ENUT TavernToPort
ENUT PortToDocks
ENUT PortToTavern
ENUT PortToOtherAreas
ENUT DocksToPort
ENUT PortToBase
ENUF WentToPortGreagorDocks //sets the temp flag version and allows global to be triggered
GiveGold(3000)
ENUF 4
MUSC PortGreagorID
NoFade
ENDA

BeginningSceneIdaGroup:
UNIT Ida 0 0 Level(20,NPC,False) [29,16] 0x0 0x0 1 REDA16R11 [0] NoAI
UNIT

ClearHubATraps:
RemoveTrapAtCoords(0, 11)
RemoveTrapAtCoords(0, 12)
RemoveTrapAtCoords(0, 13)
RemoveTrapAtCoords(20, 4)
RemoveTrapAtCoords(21, 4)
RemoveTrapAtCoords(8, 20)

RemoveTrapAtCoords(12, 5)
RemoveTrapAtCoords(18, 9)
NoFade
ENDA

GotoPortGreagor:
InitiateLoadMapHub(HubA, LuaInDaHub, PortGreagorSpawnEvent)
ENUF PortToBase
NoFade
ENDA

PortGreagorSpawnEvent:
LoadNPCUnitGroupIfEIDNotSet(WentToHubA2, 3, JaimeUnitGroup, A2Indicator)
LoadNPCUnitGroupIfEIDNotSet(WentToHubA3, 1, OndehBikoUnitGroup, A3Indicator)
LoadNPCUnitGroupIfEIDNotSet(WentToHubA6, 2, RochefortUnitGroup, A6Indicator)
NoFade
ENDA

A2Indicator:
AddTrap(RedSigilTrapID, 11, 20)
NoFade
ENDA

A3Indicator:
AddTrap(RedSigilTrapID, 23, 19)
NoFade
ENDA

A6Indicator:
AddTrap(RedSigilTrapID, 20, 14)
NoFade
ENDA

OndehBikoUnitGroup:
UNIT Ondeh 0 0 Level(4,NPC,True) [23,19] 0x0 0x0 0 0 [ShiningBow,IronBow,Vulnerary] NoAI
UNIT Biko 0 0 Level(1,NPC,True) [24,19] 0x0 0x0 0 0 [IronLance,Vulnerary] NoAI
UNIT

RochefortUnitGroup:
UNIT Rochefort 0 0 Level(1,NPC,False) [20,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT

JaimeUnitGroup:
UNIT Jaime PegasusRider 0 Level(2,NPC,False) [11,20] 0 0 0 0 [Onigiri, IronSword] NoAI
UNIT

LuaInDaHub:
UNIT Lua Captain 0 Level(10,Ally,False) [20,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaInDaHubFirstTime:
UNIT Lua Captain 0 Level(10,Ally,False) [16,9] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaInDaTreasureRoom:
UNIT Lua Captain 0 Level(10,Ally,False) [39,24] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaOnDaStairs:
UNIT Lua Captain 0 Level(10,Ally,False) [5,13] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

RefreshLua:
SVAL s1 0x42
UNSET_CONDITION Lua
ENUF Ch2ThisIsAChapter
NoFade
ENDA

Units1:
UNIT Lua 0 0 Level(10,Ally,False) [15,12] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Orlando 0 0 Level(11,Ally,False) [28,11] 0 0 0 0 [SilverKnife, Thani, Vulnerary] NoAI
//Lua's Group - 7
UNIT Rahel 0 0 Level(11,Ally,False) [15,10] 0 0 0 0 [IronBow, Vulnerary] NoAI
LoadUnit(16,10)
LoadUnit(16,11)
LoadUnit(16,12)
LoadUnit(17,10)
LoadUnit(17,11)
LoadUnit(17,12)


//Orlando's Group - 5
LoadUnit(27,10)
LoadUnit(27,12)
LoadUnit(26,10)
LoadUnit(26,11)
LoadUnit(26,12)
UNIT

Traps1:
BlueSigil(1,0)
BlueSigil(2,0)
BlueSigil(3,0)
BlueSigil(7,4)
BlueSigil(5,13)
BlueSigil(0,17)
BlueSigil(0,18)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

AddChapter2Traps:
AddTrap(BlueSigilTrapID,1,0)
AddTrap(BlueSigilTrapID,2,0)
AddTrap(BlueSigilTrapID,3,0)
AddTrap(BlueSigilTrapID,7,4)
AddTrap(BlueSigilTrapID,5,13)
AddTrap(BlueSigilTrapID,0,17)
AddTrap(BlueSigilTrapID,0,18)
NoFade
ENDA

AddTreasureRoomTraps:
AddTrap(BlueSigilTrapID,39,23)
AddTrap(BlueSigilTrapID,39,24)
NoFade
ENDA

ClearChapter2Traps:
RemoveTrapAtCoords(1,0)
RemoveTrapAtCoords(2,0)
RemoveTrapAtCoords(3,0)
RemoveTrapAtCoords(7,4)
RemoveTrapAtCoords(5,13)
RemoveTrapAtCoords(0,17)
RemoveTrapAtCoords(0,18)
RemoveQuestSigil(WentToHubA1, 1, 17, 11)
NoFade
ENDA

ClearTreasureRoomTraps:
RemoveTrapAtCoords(39,23)
RemoveTrapAtCoords(39,24)
NoFade
ENDA


BeginningScene:
UnsetAllHubChapterFlags
ENUF LuaInAHubFlag
CALL ClearChapter2Traps
TILECHANGE 0
TILECHANGE 3
ENUT MainRoomToOtherRooms
ENUT TreasureRoomToMainRoom
ENUT LuaQuartersToMainRoom
ENUT LibraryToMainRoom
ENUT MainRoomToPort
//these are all set for the objective to work
ENUT WindUpFlag1
ENUT WindUpFlag2
ENUT WindUpFlag3
ENUT ReadyForTakeoffFlag
ENUT Ch2ThisIsAChapter
ENUT RahelOrlandoHubATalkFlag
ENUT CarmenXimoHubATalkFlag
FADI 10
LoadPlayerUnits(NewPlayerGroup)
LoadEnemies(IgniswordsGroup)
LoadEnemies(TORCHGroupBottom)
LoadEnemies(TORCHGroupLeft)
LoadEnemies(TORCHMagiDivisionGroup)
AddTrap(RedSigilTrapID,18,10)
AddTrap(RedSigilTrapID,18,11)
AddTrap(RedSigilTrapID,18,12)
AddTrap(RedSigilTrapID,25,10)
AddTrap(RedSigilTrapID,25,11)
AddTrap(RedSigilTrapID,25,12)
GotoPrepScreen
NoFade
ENDA

NewPlayerGroup:
UNIT Orlando 0 0 Level(11,Ally,False) [15,11] 0 0 0 0 [SilverKnife, Thani, Vulnerary] NoAI
UNIT Rahel 0 0 Level(1,Ally,False) [16,11] 0 0 0 0 [Longbow, IronBow, Vulnerary] NoAI
UNIT

IgniswordsGroup:
UNIT Ignacio 0 0 Level(11,Enemy,False) [36,2] 0 0 0 0 [Elfire, KillingEdge, KnightsPlate] GroupFiveAI
UNIT Igniswords Monk Ignacio Level(8,Enemy,True) [32,0] 0 0 0 0 [Fire, Luna] GroupFiveAI
UNIT Igniswords Archer Ignacio Level(8,Enemy,True) [33,2] 0 0 0 0 [Longbow] GroupFiveAI

UNIT Igniswords Monk Ignacio Level(8,Enemy,True) [32,10] 0 0 0 0 [Fire] GroupOneAI
UNIT Igniswords Warden Ignacio Level(8,Enemy,True) [31,11] 0 0 0 0 [SteelSword, IronLance] GroupOneAI
UNIT Igniswords Monk Ignacio Level(8,Enemy,True) [32,12] 0 0 0 0 [Fire] GroupOneAI

UNIT Igniswords Shaman Ignacio Level(8,Enemy,True) [36,13] 0 0 0 0 [Elfire] BossAI
UNIT Igniswords Mage Ignacio Level(8,Enemy,True) [37,13] 0 0 0 0 [Thunder] BossAI

UNIT Igniswords Shaman Ignacio Level(8,Enemy,True) [36,8] 0 0 0 0 [Nosferatu] BossAI
UNIT Igniswords Shaman Ignacio Level(8,Enemy,True) [37,8] 0 0 0 0 [Elfire] BossAI
UNIT

TORCHGroupLeft:
UNIT Hugo 0 Hugo Level(12, Enemy, True) [2,1] 0 0 0 0 [SilverSword, BravesCrest] AttackWhenInRangeAI
UNIT TorchUnit Soldier Hugo Level(9,Enemy,True) [2,0] 0 0 0 0 [Pike] AttackInTwoAI

UNIT TorchUnit2 GriffinRider Hugo Level(9, Enemy, True) [11,4] 0 0 0 0 [IronLance] GoForThroneAI

UNIT TorchUnit Soldier Hugo Level(9,Enemy,True) [11,9] 0 0 0 0 [IronLance] PursueWithoutHeedAI
UNIT TorchUnit Mercenary Hugo Level(9,Enemy,True) [12,14] 0 0 0 0 [IronSword] PursueWithoutHeedAI
UNIT TorchUnit Soldier Hugo Level(9,Enemy,True) [10,12] 0 0 0 0 [IronLance] PursueWithoutHeedAI

UNIT TorchUnit Fighter Hugo Level(9,Enemy,True) [6,14] 0 0 0 0 [SteelAxe] GroupTenAI
UNIT TorchUnit Mercenary Hugo Level(9,Enemy,True) [2,12] 0 0 0 0 [SteelSword] GroupTenAI

UNIT TorchUnit Mercenary Hugo Level(9,Enemy,True) [1,0] 0 0 0 0 [SteelSword] GroupElevenAI
UNIT TorchUnit Archer Hugo Level(9,Enemy,True) [4,5] 0 0 0 0 [IronBow] GroupElevenAI

UNIT TorchUnit Fighter Hugo Level(9,Enemy,True) [3,0] 0 0 0 0 [SteelAxe] GroupTwoAI
UNIT TorchUnit Archer Hugo Level(9,Enemy,True) [0,5] 0 0 0 0 [IronBow] GroupTwoAI
UNIT

TORCHGroupBottom:
UNIT ElodiaBoss 0 0 Level(15, Enemy, False) [38, 23] 0 0 0 0 [Baselard, SilverLance, KnightRing] GroupSixAI
UNIT TorchUnit AxeCav ElodiaBoss Level(10,Enemy,True) [33,22] 0 0 0 0 [SteelAxe] GroupSixAI
UNIT TorchUnit LanceCav ElodiaBoss Level(10,Enemy,True) [33,23] 0 0 0 0 [IronLance] GroupSixAI

UNIT TorchUnit LanceCav ElodiaBoss Level(10,Enemy,True) [27,23] 0 0 0 0 [SteelLance, Javelin] GroupSevenAI
UNIT TorchUnit BowCav ElodiaBoss Level(10,Enemy,True) [28,22] 0 0 0 0 [SteelBow] GroupSevenAI
UNIT TorchUnit BowCav ElodiaBoss Level(10,Enemy,True) [28,24] 0 0 0 0 [SteelBow] GroupSevenAI

UNIT TorchUnit AxeCav ElodiaBoss Level(10,Enemy,True) [18,22] 0 0 0 0 [SteelAxe] GroupEightAI
UNIT TorchUnit LanceCav ElodiaBoss Level(10,Enemy,True) [15,25] 0 0 0 0 [SteelLance] GroupEightAI

UNIT TorchUnit2 SwordCav ElodiaBoss Level(10,Enemy,True) [39,22] 0 0 0 0 [IronSword] GoForThroneAI
UNIT TorchUnit2 AxeCav ElodiaBoss Level(10,Enemy,True) [39,24] 0 0 0 0 [IronAxe] GoForThroneAI

UNIT

TORCHMagiDivisionGroup:
//name this guy
UNIT Juliana 0 ElodiaBoss Level(12,Enemy,True) [0,24] 0 0 0 0 [Arcfire, MagesRing] GroupTwelveAI
UNIT TorchUnit Monk ElodiaBoss Level(9,Enemy,True) [1,22] 0 0 0 0 [Luna] GroupTwelveAI
UNIT TorchUnit Shaman ElodiaBoss Level(9,Enemy,True) [4,22] 0 0 0 0 [Nosferatu] GroupTwelveAI
UNIT TorchUnit Mage ElodiaBoss Level(9,Enemy,True) [3,23] 0 0 0 0 [Elfire] GroupTwelveAI
UNIT


EndingScene:
GOTO_TITLE
NoFade
ENDA


Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN