EventPointerTable(HubA_4EventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units2
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

//flags
//9 - juan talk flag
//10 - pirate boss death quote flag
//11 - rebel boss death quote flag
//12 - early arrival flag (if pirate boss is killed before rebels show up)
//13 - funny archer death quote flag
//14 - turned on by PirateBossDefeat misc event
//15 - turned on by RebelBossDefeat misc event
//16 - pirate boss battle quote
//17 - rebel boss battle quote
//18 - turned on by JuanReaction misc event
//19 - rebel arrival event flag (can't share with other turn based events for whatever reason)

TurnBasedEvents:
TurnEventPlayer(0, SurvivorsTalk, 1)
TurnEventPlayer(0, RebelPremonition, 2)
TurnEventPlayer(0, ThiefArrival, 4)
TurnEventPlayer(19, RebelArrival, 3, 5)
TurnEventPlayer(12, RebelWarning1, 3)
TurnEventPlayer(12, RebelWarning2, 5)
TurnEventPlayer(12, RebelWarning3, 6)
END_MAIN

SurvivorsTalk:
Music(ABlackheartsPrideID)
CameraCursor(9,16)
SetBackground(Inn)
Text(A4_TurnEvent0)
EraseBackground
NoFade
ENDA

RebelPremonition:
SpawnReinforcements(RebelScoutSpawn)
ENUN
MOVE 10 Rebel [20,6]
ENUN
CameraCursor(Rebel)
Text(A4_TurnEvent1_A)
MOVE 20 Rebel [23,11]
ENUN
REMOVEUNIT Rebel
CameraCursor(Lua)
Text(A4_TurnEvent1_B)
NoFade
ENDA

ThiefArrival:
SpawnReinforcements(ThiefSpawn)
AddTrap(RedSigilTrapID,0,22)
NoFade
ENDA

RebelArrival:
CHECK_TURNS
SVAL s1 7
BEQ 1 s1 sC  //if the turn count is 7, then we branch to the rebel spawn event
	CHECK_EVENTID 10
	BNE 1 s0 sC //if the pirate boss is dead, play the event early
		ENUF 19 //unset the rebel arrival flag if neither of these are true
		GOTO 2
LABEL 1
	MUSC 0x7FFF
	CAMERA_CENTERED [23,16]
	TILECHANGE 1
	STAL 30
	TILECHANGE 2
	SOUN 0x02D7 //snag falling sound effect
	STAL 15
	FADI 15
	SpawnReinforcements(RebelSpawn)
	FADU 15
	ENUN
	Text(A4_TurnEvent3_A)
	Music(AvastMeHeartiesID)
	//if pirate boss is still alive
	CHECK_EXISTS Stingray //returns 1 if true (thus is still alive)
	BEQ 8 s0 sC
		Text(A4_TurnEvent3_B_A) //if he's alive, play this quote
		GOTO 9
	LABEL 8
			Text(A4_TurnEvent3_B_B) //if he's dead, play this instead
	LABEL 9
	CameraCursor(Ramiro)
	Text(A4_TurnEvent4)
	SetAI(Razorblood,0,0,4,0)
	CHECK_EXISTS Stingray //play this stuff if the pirate boss is still alive, else skip to label 10
	BEQ 10 s0 sC
		CameraCursor(Stingray)
		Text(A4_TurnEvent4_B)
		SetAI(Stingray,0,0,4,0)
	LABEL 10
	ENUT 12
	SetChapterObjectiveText(ChA4GoalWindowB, ChA4StatusObjectiveB)
LABEL 2
NoFade
ENDA

RebelWarning1:
ENUF 12 //turn this off, as the player could still potentially kill the boss early
CHECK_EVENTID 10 //play this event if the boss hasn't been killed
BNE 4 s0 sC
	CAMERA_CENTERED [23,25]
	STAL 30
	TILECHANGE 3
	CameraCursor(Lua)
	Text(A4_TurnEvent2_A)
	CameraCursor(Stingray)
	Text(A4_TurnEvent2_B)
LABEL 4
NoFade
ENDA

RebelWarning2:
ENUF 12 //turn this off, as the player could still potentially kill the boss early
CHECK_EVENTID 10 //play this event if the boss hasn't been killed
BNE 5 s0 sC
	CAMERA_CENTERED [23,20]
	STAL 30
	TILECHANGE 4
LABEL 5
NoFade
ENDA

RebelWarning3:
ENUF 12 //turn this off, as the player could still potentially kill the boss early
CHECK_EVENTID 10 //play this event if the boss hasn't been killed
BNE 6 s0 sC
	CAMERA_CENTERED [23,16]
	STAL 30
	TILECHANGE 1
LABEL 6
NoFade
ENDA

CharacterBasedEvents:
CharacterEventBothWays(9, JuanRecruitment, Lua, Juan)
END_MAIN

JuanRecruitment:
MUSS WelcomeAboardMateyID
SetAI(Juan,0,0,0,0)
Text(ChA4JuanRecruitText)
CUSA Juan
MURE 2
NoFade
ENDA

JuanReaction:
CHECK_ALIVE Juan
BEQ 7 s0 sC //if he's dead, branch and don't check allegiance
CHECK_ALLEGIANCE Juan //returns 0 if he's a player, 2 if an NPC
BNE 7 s0 sC //skip to label 7 if Juan is not a player unit
	CHECK_TURNS
	SVAL s1 2
	BNE 7 s1 sC //skip if not turn 2
		CHECK_ACTIVEID Lua //returns 1 if Lua, 0 if not
		BEQ 7 s0 sC //skip if Lua is not the active unit
			CameraCursor(Juan)
			Text(A4_JuanReaction_Optional)
LABEL 7
NoFade
ENDA

LocationBasedEvents:
DurabilityChest(SkillScroll,CritthriftID,12,22)
Chest(TakeAim, 10, 1)
END_MAIN

MiscBasedEvents:
AFEV 14 PirateBossDefeat 10
AFEV 15 RebelBossDefeat 11
AFEV 18 JuanReaction 13
CauseGameOverIfLordDies
END_MAIN

PirateBossDefeat:
CHECK_EVENTID 11 //check if the rebelBoss death quote has been played
BEQ 1 s0 sC
	CALL EndingScene
LABEL 1
NoFade
ENDA

RebelBossDefeat:
CHECK_EVENTID 10 //check if the pirateBoss death quote has been played
BEQ 1 s0 sC
	CALL EndingScene
LABEL 1
NoFade
ENDA

Units1:
UNIT Lua Captain 0x0 Level(1, Ally, False) [6,0] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [7,0] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [5,1] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [6,1] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [7,1] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [5,2] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [6,2] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [5,3] 0 0 0 0 [Vulnerary] NoAI
UNIT

Units2:
UNIT Lua Captain 0x0 Level(1, Ally, False) [6,0] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [7,0] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [5,1] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [6,1] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [7,1] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [5,2] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [6,2] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [5,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [4,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Lua Captain 0x0 Level(1, Ally, False) [8,0] 0 0 0 0 [Vulnerary] NoAI
UNIT

NPCGroup:
UNIT Juan Thief 0 Level(4, NPC, True) [13,4] 0 0 0 0 [IronDagger, IronKnife, Vulnerary] BossAI
UNIT

EnemyGroup:
//debug
//UNIT Stingray Deserter 0 Level(5,Enemy,True) [9,0] 0 0 0 0 [Javelin, SteelLance] BossAI
//UNIT Ramiro SwordCav 0 Level(5, Enemy, True) [10,0] 0 0 0 0 [FlameSword, GlassShield] ChargeAI
//turn 1
UNIT Razorblood Deserter 0 Level(3, Enemy, True) [6,5] 0 0 0 0 [IronLance] AttackInRangeAI
UNIT Razorblood Mercenary 0 Level(3, Enemy, True) [8,4] 0 0 0 0 [IronSword] AttackInRangeAI
UNIT Razorblood Vanguard 0 Level(3, Enemy, True) [11,2] 0 0 0 0 [IronAxe] AttackInRangeAI
//turn 2
UNIT Razorblood Brigand 0 Level(3, Enemy, True) [9,8] 0 0 0 0 [IronAxe] AttackInRangeAI
UNIT A4FunnyArcher Hunter 0 Level(3, Enemy, False) [12,12] 0 0 0 0 [IronBow] ChargeAI //dies in a gag for Juan's recruitment convo
UNIT Razorblood Shaman 0 Level(3, Enemy, True) [9,11] 0 0 0 0 [Elfire, Tonic, TealGem] ChargeAI //speed tonic guy
UNIT Razorblood Ninja 0 Level(3, Enemy, True) [5,12] 0 0 0 0 [IronKnife] ChargeAI
//turn 3
UNIT Razorblood Hunter 0 Level(3, Enemy, True) [8,13] 0 0 0 0 [IronBow] GroupOneAI //KEEP AN EYE ON THIS GUY'S POSITIONING
UNIT Razorblood Mercenary 0 Level(3, Enemy, True) [5,16] 0 0 0 0 [IronSword] GroupOneAI
UNIT Razorblood Fighter 0 Level(3, Enemy, True) [12,13] 0 0 0 0 [IronAxe] GroupOneAI
//turn 4
UNIT Razorblood Deserter 0 Level(3, Enemy, True) [12,17] 0 0 0 0 [IronLance] GroupOneAI //grouped with the turn3 hunter
UNIT Razorblood Ninja 0 Level(3, Enemy, True) [5,15] 0 0 0 0 [IronDagger,IronKnife] AttackInRangeAI
UNIT Razorblood Vanguard 0 Level(3, Enemy, True) [10,13] 0 0 0 0 [IronAxe] GroupTwoAI
//turn 5
UNIT Razorblood Brigand 0 Level(3, Enemy, True) [11,17] 0 0 0 0 [IronAxe] AttackInRangeAI
UNIT Razorblood Deserter 0 Level(3, Enemy, True) [8,17] 0 0 0 0 [IronLance] GroupTwoAI
UNIT Razorblood Hunter 0 Level(3, Enemy, True) [13,19] 0 0 0 0 [IronBow, PureWater] GroupTwoAI
//turn 6
UNIT Razorblood Myrmidon 0 Level(3, Enemy, True) [7,20] 0 0 0 0 [IronSword] GroupThreeAI
UNIT Razorblood Shaman 0 Level(3, Enemy, True) [6,22] 0 0 0 0 [Elfire, Tonic] GroupThreeAI //def tonic
UNIT Razorblood Vanguard 0 Level(3, Enemy, True) [11,21] 0 0 0 0 [IronAxe] GroupFourAI
UNIT Razorblood Mage 0 Level(3, Enemy, True) [10,22] 0 0 0 0 [Fire, Surge, Vulnerary] GroupFourAI
//turn 7
UNIT Razorblood Fighter 0 Level(3, Enemy, True) [9,23] 0 0 0 0 [IronAxe] GroupFiveAI //first on the chopping block if the end is too much
UNIT Razorblood Hunter 0 Level(3, Enemy, True) [8,23] 0 0 0 0 [SteelBow] GroupFiveAI
UNIT Stingray Deserter 0 Level(5,Enemy,True) [9,22] 0 0 0 0 [Javelin, SteelLance] GroupFiveAI
UNIT

RebelSpawn:
UNIT Ramiro SwordCav 0 Level(5, Enemy, True) [20,16] 0 0 0 0 [FlameSword, GlassShield] ChargeAI
UNIT Rebel Soldier 0 Level(3, Enemy, True) [18,20] 0 0 0 0 [SteelLance] ChargeAI
UNIT Rebel Warden 0 Level(3, Enemy, True) [18,14] 0 0 0 0 [Javelin, SteelSword] ChargeAI
UNIT Rebel Warden 0 Level(3, Enemy, True) [20,12] 0 0 0 0 [Javelin, SteelSword] ChargeAI
UNIT Rebel Archer 0 Level(3, Enemy, True) [22,15] 0 0 0 0 [Longbow] ChargeAI
UNIT Rebel Fighter 0 Level(3, Enemy, True) [20,19] 0 0 0 0 [IronAxe, HandAxe] ChargeAI
UNIT Rebel GriffinRider 0 Level(3, Enemy, True) [21,10] 0 0 0 0 [Pike] ChargeAI
UNIT

RebelScoutSpawn:
UNIT Rebel GriffinRider 0 Level(3, NPC, True) [20,0] 0 0 0 0 [IronSword] AttackInRangeAI
UNIT

ThiefSpawn:
UNIT Razorblood Thief 0 Level(3, Enemy, True) [0,22] 0 0 0 0 [IronKnife, ChestKey] ThiefAI
UNIT

CutsceneGreens:
UNIT Rebel Gunner 0 Level(3, NPC, True) [7,19] 0 0 0 0 [SteelLance] ChargeAI //imperial captain
UNIT Rebel Soldier 0 Level(3, NPC, True) [8,18] 0 0 0 0 [SteelLance] ChargeAI //imperial recruit
UNIT Incendivis Soldier 0 Level(3, NPC, True) [8,18] 0 0 0 0 [SteelLance] ChargeAI//first imperial who gets owned
//second imperial who gets owned
//
//
//
UNIT

CutsceneReds:
UNIT Stingray Deserter 0 Level(5,Enemy,True) [0,22] 0 0 0 0 [Javelin, SteelLance] GroupFiveAI //pirate captain
UNIT Razorblood Fighter 0 Level(3, Enemy, True) [9,23] 0 0 0 0 [IronAxe] GroupFiveAI
UNIT A4FunnyArcher Hunter 0 Level(3, Enemy, True) [12,12] 0 0 0 0 [IronBow] ChargeAI
//lotsa pirates who will appear later on
//
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
WEATHER 7
SetBackground(Inn)
Music(StillZephyrsID) //could probably use something else similar here once more event music gets added?
TEXTSTART
TEXTSHOW A4StartEventBase_1
TEXTEND
SOUN 0xA2 //armor knight footsteps
SOUN 0xA3
SOUN 0xA4
TEXTCONT
TEXTEND
REMA
SetBackground(Ship)
MUSC 0x47 //windy day
TEXTSTART
TEXTSHOW A4StartEventBase_2
TEXTEND
Music(SwabbieTalkID)
TEXTCONT
TEXTEND
REMA
//EraseBackground
Music(ScalleywagsOnDeckID)
//Cue a pair of scripted battles of green units getting beaten by red ones.
//gonna need 8 different greens [capt, recruit, 5 generics who survive, 2 who get yeeted]
//gonna need 3 different reds [2 who kill greens, capt] + a bunch more who just stand there [comprised of units who the player fights]
SetBackground(Ship)
TEXTSTART
TEXTSHOW A4StartEventBase_3
TEXTEND
SOUN 0xF2 //"fire shoot", supposed to be a gunfire sfx. happens mid text
TEXTCONT
TEXTEND
REMA
//EraseBackground
Music(AvastMeHeartiesID)
//have pirate captain walk up
TEXTSTART
TEXTSHOW A4StartEventBase_4
TEXTEND
FAWI 15
SOUN 0xF8 //elfire sfx, supposed to be explosion
FAWU 15
TEXTCONT
TEXTEND
REMA
//cutscene of green units managing to retreat into the hull, including the captain. door shuts just as the captain gets there.
SOUN 0xB1 //lockpick sound
Text(A4StartEventBase_5)
SetBackground(Ship)
Text(A4StartEventBase_6)
FADI 15
EraseBackground
REMOVEALL_GREEN
REMOVEALL_RED
LOAD1 1 NPCGroup
LOAD1 1 EnemyGroup
ENUN
SetUnitItemDurability(9, 11, 1, SpdTonic) 
SetUnitItemDurability(6, 21, 1, DefTonic) 
STAL 1
GotoPrepScreen
NoFade
ENDA

EndingScene:
Music(StillZephyrsID)
SetBackground(Ship)
Text(A4_EndEvent_1)
GiveGold(1200)
Music(ABlackheartsPrideID)
Text(A4_EndEvent_2)
GoToHubChapter(HubA)
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN