EventPointerTable(HubAEventsID, PointerList)

//flags for Chapter 2: cannot overlap with ones used for hub A when on this map
#define EnemyGotOnBoat 7
#define WindUpFlag1 8
#define WindUpFlag2 9
#define WindUpFlag3 10
#define ReadyForTakeoffFlag 11
#define Ch2ThisIsAChapter 12
#define IgniswordsDead 13
#define Ch2ReinforcementFlags 14

// temp flags
#define SeenHubA1Pitch 0x7
#define SeenHubA2Pitch 0x8
#define SeenHubA3Pitch 0x9
#define SeenHubA5Pitch 0xB
#define SeenHubA6Pitch 0xc

#define GotoChapter2Flag 0xe

//hub maneuvering flags
#define LibraryToMainRoom 25
#define LuaQuartersToMainRoom 26
#define MrYodsenSpokeToOnce 27
#define TavernToPort 28
#define PortToTavern 29
#define MainRoomToOtherRooms 30
#define MainRoomToPort 31
#define TreasureRoomToMainRoom 32
#define PortToDocks 33
#define PortToOtherAreas 34
#define DocksToPort 35
#define PortToBase 36
#define DurghamSpokeToOnce 37
#define TrianaSpokeToOnce 38
#define MedwynSpokeToOnce 39
#define BartzSpokeToOnce 40
//global flags
#define WentToPortGreagor 0x92
#define WentToPortGreagorDocks 0x93
#define WentToBaseAgain 0x94
#define CameBackFromDocks 0x95
#define DurghamRecruitFlag 0x96
#define HubASamsonArranRecruitFlag 0x97
#define FirstTimeInTavern 0x98
#define MrYodsenRecruitFlag 0x99
#define TavernFirstTime 0xA1

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN

CharacterBasedEvents:
CharacterEvent(WentToHubA2, GotoChapter2, Lua, Jaime)
CharacterEvent(WentToHubA3, GotoChapter3, Lua, Ondeh)
CharacterEvent(WentToHubA5, GotoChapter5, Lua, Desiderio)
CharacterEvent(WentToHubA6, GotoChapter6, Lua, Rochefort)
CharacterEvent(HubASamsonArranRecruitFlag, TalkToTriana, Lua, Triana)
CharacterEvent(HubASamsonArranRecruitFlag, TalkToMedwyn, Lua, Medwyn)
CharacterEvent(HubASamsonArranRecruitFlag, TalkToBartz, Lua, Bartz)
CharacterEvent(DurghamRecruitFlag, TalkToDurgham, Lua, Durgham)
CharacterEvent(MrYodsenRecruitFlag, TalkToMrYodsen, Lua, MrYodsen)
END_MAIN

GotoChapter1:
HubChapterFirstTimeEvent(1, SeenHubA1Pitch, HubA1Pitch)
//TutorialYesNoText(HubA1Prompt)
FancyChapterPreview(HubA_1, Castle_Hall_Night, HubA)
EVBIT_MODIFY 0
GotoHubChapterIfYes(1, HubA_1, HubA1Accepted)
ENUF WentToHubA1
NoFade
ENDA

GotoChapter2:
HubChapterFirstTimeEvent(1, SeenHubA2Pitch, HubA2Pitch)
//Text(HubA2Prompt)
FancyChapterPreview(HubA_2, Castle_Hall_Night, HubA)
EVBIT_MODIFY 0
GotoHubChapterIfYes(2, HubA_2, HubA2Accepted)
ENUF WentToHubA2
NoFade
ENDA

GotoChapter3:
HubChapterFirstTimeEvent(1, SeenHubA3Pitch, HubA3Selection_Text)
//TutorialYesNoText(HubA3Sys_Text_Mission_Select)
FancyChapterPreview(HubA_3, Castle_Hall_Night, HubA)
GotoHubChapterIfYes(2, HubA_3, HubA3Selection_Yes)
Text(HubA3Selection_No)
ENUF WentToHubA3
NoFade
ENDA

GotoChapter5:
HubChapterFirstTimeEvent(1, SeenHubA5Pitch, HubA5Pitch)
//Text(HubA5Prompt) 
FancyChapterPreview(HubA_5, Castle_Hall_Night, HubA)
ENUT SeenHubA5Pitch
EVBIT_MODIFY 0
SVAL s1 0x1
BNE 2 s1 sC
Text(HubA5Accepted)
ENUF LuaInAHubFlag
FADI 10
CUSA Desiderio
CUSA Pilar
ENUN
ASMC EyeForAnEyeResetASMC
MNC2 HubA_5
NoFade
ENDB
LABEL 2
ENUF WentToHubA5
NoFade
ENDA

GotoChapter6:
HubChapterFirstTimeEvent(1, SeenHubA6Pitch, HubA6Pitch)
//Text(HubA6Prompt) 
FancyChapterPreview(HubA_6, Castle_Hall_Night, HubA)
EVBIT_MODIFY 0
GotoHubChapterIfYes(2, HubA_6, HubA6Accepted)
ENUF WentToHubA6
NoFade
ENDA

LocationBasedEvents:
Armory(HubAArmory,16,13)
Vendor(HubAVendor,24,13)
END_MAIN

ALIGN 4
HubAArmory:
SHLI IronSword SteelSword IronLance SteelLance IronAxe SteelAxe IronBow SteelBow IronKnife SteelKnife IronDagger SteelDagger IronMusket SteelMusket

ALIGN 4
HubAVendor:
SHLI Fire Elfire Heal Mend Vulnerary (HPTonic << 8)|Tonic (StrTonic << 8)|Tonic (MagTonic << 8)|Tonic (SklTonic << 8)|Tonic (SpdTonic << 8)|Tonic (LuckTonic << 8)|Tonic  (DefTonic << 8)|Tonic (ResTonic << 8)|Tonic
ALIGN 4

MiscBasedEvents:
AREA PortToDocks PortGreagorDocks [29,15] [29,16]
AREA WentToPortGreagorDocks PortGreagorDocksFirstTime [29,15] [29,16]
AREA DocksToPort GotoPortFromDocks [0,13] [0,14]
AREA CameBackFromDocks CameBackFromDocksFirstTime [0,13] [0,14]

//AREA PortToBase BackToLuaBase [20,4] [21,4]
AREA WentToBaseAgain BackToLuaBaseFirstTime [20,4] [21,4]

AREA PortToTavern FromPortToTavern [8,20] [8,20]
AREA TavernToPort ToPortFromTavern [8,20] [8,20] 

AREA GotoChapter2Flag AllDoneWithHubA [16,10] [16,10]

AREA WentToHubA1 GotoChapter1 [12,5] [12,5]
AREA WentToHubA4 GotoChapter4 [18, 9] [18, 9]
AFEV 0 RefreshLua 0
CauseGameOverIfLordDies
END_MAIN

AllDoneWithHubA:
SET_ACTIVE 0
FADI 10
MNC2 Chapter2 
NoFade
ENDA

Traps1:
BlueSigil(0, 11)
BlueSigil(0, 12)
BlueSigil(0, 13)
BlueSigil(20, 4)
BlueSigil(21, 4)
BlueSigil(29, 15)
BlueSigil(29, 16)
BlueSigil(8, 20)

RedSigil(12,5)
RedSigil(18, 9)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
UNIT Lua Captain 0 Level(10,Ally,False) [13,11] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

BeginningScene:
// only here for chapter A1 testing
ENUT GotoChapter2Flag
ENUT LuaInAHubFlag
ENUT PortToOtherAreas
BranchIfHubNotVisited(1, BeginningSceneFirstTimeCave)
ENUT DocksToPort
ENUT TavernToPort
CALL PortSpawnEvent
CheckHowManyHubChaptersCompleted
SVAL s1 6 //if 5 chapters completed
BLT 2 sC s1 
    AddTrap(GreenSigilTrapID, 16, 10) //green means go
    CenterTextbox(HubAGoTime)
    ENUF GotoChapter2Flag
LABEL 2
GotoPrepScreenHub
ENUN
NoFade 
ENDA

EndingScene:
NoFade
MNC2 Chapter2
ENDA


//CHARACTER EVENTS HERE

TalkToTriana:
CheckHowManyHubChaptersCompleted
SVAL s1 1
BLE 1 sC s1 //if 1 or less hub chapters completed, triana recruitable so branch to that
    Text(TrianaUnrecruitableText) //triana's statement that she's found work and isn't interested anymore
    GOTO 5
LABEL 1
HubFirstTimeTalkToUnit(TrianaSpokeToOnce, 2, TrianaFirstTimeText)
LABEL 2
TutorialYesNoText(RecruitTrianaChoice)
HubRecruitUnit(Triana, 3, TrianaRecruitedText, 4)
LABEL 3
Text(TrianaRejectedText)
LABEL 5
ENUF HubASamsonArranRecruitFlag
LABEL 4
NoFade
ENDA

TalkToMedwyn:
CheckHowManyHubChaptersCompleted
SVAL s1 3
BLE 1 sC s1 //if less than 4, part of medwyn's time
    Text(MedwynUnrecruitableText) //medwyn's statement that he's drunk off his ass so he's unintelligible
    GOTO 5
LABEL 1
SVAL s1 2
BGE 0 sC s1 //if 2 or 3, then it's medwyn's rare moment of clarity, so skip
    Text(MedwynUnrecruitableText) 
    GOTO 5
LABEL 0
HubFirstTimeTalkToUnit(MedwynSpokeToOnce, 2, MedwynFirstTimeText)
LABEL 2
TutorialYesNoText(RecruitMedwynChoice)
HubRecruitUnit(Medwyn, 3, MedwynRecruitedText, 4)
LABEL 3
Text(MedwynRejectedText)
LABEL 5
ENUF HubASamsonArranRecruitFlag
LABEL 4
NoFade
ENDA

TalkToBartz:
CheckHowManyHubChaptersCompleted
SVAL s1 4
BGE 1 sC s1 //if 4 hub chapters completed, bartz available for recruit so skip
    Text(BartzUnrecruitableText) //bartz's statement that he's waiting for a job
    GOTO 5
LABEL 1
HubFirstTimeTalkToUnit(BartzSpokeToOnce, 2, BartzFirstTimeText)
LABEL 2
TutorialYesNoText(RecruitBartzChoice)
HubRecruitUnit(Bartz, 3, BartzRecruitedText, 4)
LABEL 3
Text(BartzRejectedText)
LABEL 5
ENUF HubASamsonArranRecruitFlag
LABEL 4
NoFade
ENDA


TalkToDurgham:
HubFirstTimeTalkToUnit(DurghamSpokeToOnce, 1, DurghamFirstTimeText)
LABEL 1
TutorialYesNoText(RecruitDurghamChoice)
HubRecruitUnit(Durgham, 2, DurghamRecruitedText, 3)
LABEL 2
HubRejectUnit(DurghamRejectedText, DurghamRecruitFlag) //unsets this flag if you decided not to recruit
LABEL 3
NoFade
ENDA

TalkToMrYodsen:
CheckHowManyHubChaptersCompleted
SVAL s1 6
BNE 0 s1 sC //if not all hub chapters have been completed, branch to 0
    HubFirstTimeTalkToUnit(MrYodsenSpokeToOnce, 1, MrYodsenFirstTimeFreeText)
    SVAL s1 0
    GOTO 1
LABEL 0
    HubFirstTimeTalkToUnit(MrYodsenSpokeToOnce, 4, MrYodsenFirstTimeText)
LABEL 4
    CalculateYodsenPrice
    SADD s1 s0 sC
    CHECK_MONEY
    BLE 1 s1 sC //if you have more money than the cost, branch to 1
        Text(MrYodsenTellsLuaShesBrokeText)
        GOTO 5
LABEL 1
    GetTextFromMemSlot1
    Text(RecruitMrYodsenChoice)
    SADD s3 s1 s0 //moves how much gold to take to s3 
    HubRecruitYodsen(MrYodsen, 2, MrYodsenRecruitedText, 3)
LABEL 2
Text(MrYodsenRejectedText)
LABEL 5
ENUF MrYodsenRecruitFlag //unsets this flag if you decided not to recruit
LABEL 3
NoFade
ENDA

// AREA EVENTS HERE

FromPortToTavern:
InitiateTileChangeInHub(ClearHubATraps, AddTavernTraps, 1, LuaInDaTavern, TavernGreensSpawnEvent)
ENUF TavernToPort
NoFade
ENDA

ToPortFromTavern:
InitiateTileRevertInHub(ClearTavernTraps, AddHubATraps, 1, LuaInFrontOfDaTavern, PortSpawnEvent)
ENUF PortToTavern
NoFade
ENDA

PortGreagorDocksFirstTime:
UnsetAllHubChapterFlags
InitiateTileChangeInHub(ClearHubATraps, AddDockTraps, 0, LuaAtDaDocks, DocksSpawnEvent)
RemoveTrapAtCoords(8,22)
ENUT DocksToPort
ENUF CameBackFromDocks
ENUT WentToHubA5
Text(HubABeginningScene4)
Text(HubABeginningScene5)
NoFade
ENDA

PortGreagorDocks:
InitiateTileChangeInHub(ClearHubATraps, AddDockTraps, 0, LuaAtDaDocks, DocksSpawnEvent)
ENUF DocksToPort
NoFade
ENDA

CameBackFromDocksFirstTime:
ENUF WentToHubA5
InitiateTileRevertInHub(CUSADockBlues, ClearDockTraps, AddHubABackToBaseTraps, 0, LuaBackFromDaDocks, PortSpawnEventFirstTime)
SetAllHubChapterFlags
ENUT PortToBase
ENUF WentToBaseAgain
NoFade
ENDA

CUSADockBlues:
CUSA Desiderio
CUSA Pilar
NoFade
ENDA

AddHubABackToBaseTraps:
AddTrap(BlueSigilTrapID,20, 4)
AddTrap(BlueSigilTrapID,21, 4)
NoFade
ENDA

GotoPortFromDocks:
InitiateTileRevertInHub(CUSADockBlues, ClearDockTraps, AddHubATraps, 0, LuaBackFromDaDocks, PortSpawnEvent)
ENUF PortToDocks
NoFade
ENDA

BackToLuaBase:
InitiateLoadMapHub(Chapter2, ClearHubATraps, LuaFromDaPortInDaBase, MainRoomSpawnEvent)
NoFade
ENDA

MainRoomSpawnEvent:
LoadPlayerUnits(OrlandoRahelUnitGroup)
NoFade
ENDA

LuaFromDaPortInDaBase:
LoadPlayerUnits(LuaBackFromPortGroup)
NoFade
ENDA

OrlandoRahelUnitGroup:
UNIT Orlando 0 0 Level(12,NPC,False) [6,16] 0x0 0x0 0 0 [Bloodletter, Thani] NoAI
UNIT Rahel Civilian_F_2 0 Level(1,NPC,False) [6,18] 0x0 0x0 0 0 [0x0] NoAI
UNIT

LuaBackFromPortGroup:
UNIT Lua Captain 0 Level(10,Ally,False) [2,0] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

BackToLuaBaseFirstTime:
FADI 10
SET_ACTIVE 0
LoadPlayerUnits(NewUnits)
STAL 10
RemoveAll
UnsetAllHubChapterFlags
ENUF PortToDocks
ENUF PortToBase
ENUF PortToTavern
ENUF WindUpFlag1
ENUF WindUpFlag2
ENUF WindUpFlag3
ENUF ReadyForTakeoffFlag
ENUF IgniswordsDead
CALL PortSpawnEvent  //unset all chapter flags and port flags so you can walk around
CALL AddHubATraps
LoadPlayerUnits(LuaInDaHub)
CAMERA_CENTERED Lua
SetBackground(Fort)
Text(HubABeginningScene6)
Text(HubABeginningScene6_2)
EraseBackground
MUSC PortGreagorID
NoFade
ENDA

LuaInDaHub:
UNIT Lua Captain 0 Level(10,Ally,False) [20,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

RefreshLua:
SVAL s1 0x42
UNSET_CONDITION Lua
NoFade
ENDA

GotoChapter4:
//TutorialYesNoText(HubA4Prompt) 
FancyChapterPreview(HubA_4, Castle_Hall_Night, HubA)
GotoHubChapterIfYes(1, HubA_4)
ENUF WentToHubA4
NoFade
ENDA



LuaFirstTripCaveGroup:
UNIT Lua Captain 0 Level(10,Ally,False) [9,12] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT



BeginningSceneFirstTimeCave:
SetBackground(Ship)
MUSC StillZephyrsID
Text(HubABeginningScene1)
SetBackground(Fort)
Text(HubABeginningScene2)

ENUT MainRoomToPort //sets immediate temp flag that i don't want triggered
ENUT WentToPortGreagorDocks //sets future flag EIDs that I don't want triggered yet
ENUT CameBackFromDocks
ENUT WentToBaseAgain
ENUT MainRoomToOtherRooms
ENUT TreasureRoomToMainRoom
ENUT LuaQuartersToMainRoom
ENUT LibraryToMainRoom
ENUT WindUpFlag1
ENUT WindUpFlag2
ENUT WindUpFlag3
ENUT ReadyForTakeoffFlag
ENUT IgniswordsDead

FADI 10
LOMA Chapter2
RemoveAll
CAMERA_CENTERED [9,12]
CALL ClearChapter2Traps
EraseBackground
LOAD1 0 LuaFirstTripCaveGroup
ENUN
ENUT 4
CenterTextbox(HubTutorial)
NoFade
ENDB

ClearChapter2Traps:
RemoveTrapAtCoords(7,4)
RemoveTrapAtCoords(5,13)
RemoveTrapAtCoords(0,17)
RemoveTrapAtCoords(0,18)
NoFade
ENDA

LuaAtDaDocks:
UNIT Lua Captain 0 Level(10,Ally,False) [0,14] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaBackFromDaDocks:
UNIT Lua Captain 0 Level(10,Ally,False) [29,16] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaInFrontOfDaTavern:
UNIT Lua Captain 0 Level(10,Ally,False) [8,20] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaInDaTavern:
UNIT Lua Captain 0 Level(10,Ally,False) [8,20] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

NewUnits:
UNIT Estelle Troubadour 0 Level(3,Ally,False) [0,5] 0 0 0 0 [Heal, Torch] NoAI
UNIT Hermenegildo Myrmidon 0 Level(1,Ally,False) [14,5] 0 0 0 0 [IronSword, LightBrand, Vulnerary] NoAI
UNIT

DurghamUnitGroup:
UNIT Durgham LanceCav 0 Level(5,NPC,False) [1,20] 0 0 0 0 [IronLance, SteelLance, Vulnerary] NoAI
UNIT

SamsonArranUnitGroup:
UNIT Triana Shaman_F 0 Level(4,NPC,False) [3,14] 0 0 0 0 [Surge, Elfire, Vulnerary] NoAI
UNIT Medwyn Warden 0 Level(6,NPC,False) [5,14] 0 0 0 0 [SteelSword, Javelin, PureWater] NoAI
UNIT Bartz Deserter 0 Level(8,NPC,False) [7,14] 0 0 0 0 [SteelLance, Vulnerary] NoAI
UNIT

MrYodsenUnitGroup:
UNIT MrYodsen Gunner 0 Level(6,NPC,False) [12,16] 0 0 0 0 [SteelMusket, Mortar, Vulnerary, PureWater] NoAI
UNIT

JaimeUnitGroup:
UNIT Jaime PegasusRider 0 Level(2,NPC,False) [11,20] 0 0 0 0 [SteelSword, IronSword] NoAI
UNIT

OndehBikoUnitGroup:
UNIT Ondeh 0 0 Level(4,NPC,True) [23,19] 0x0 0x0 0 0 [ShiningBow,IronBow,Vulnerary] NoAI
UNIT Biko 0 0 Level(1,NPC,True) [24,19] 0x0 0x0 0 0 [IronLance,Vulnerary] NoAI
UNIT

RochefortUnitGroup:
UNIT Rochefort 0 0 Level(1,NPC,False) [20,14] 0x0 0x0 0 0 [SteelMusket] NoAI
UNIT

DesiderioPilarGroup:
UNIT Desiderio 0 0 Level(4,Ally,True) [8,22] 0x0 0x0 0 0 [0x0] NoAI
UNIT Pilar 0 0 Level(1,Ally,True) [4,12] 0x0 0x0 0 0 [0x0] NoAI
UNIT

DocksSpawnEvent:
CALL DocksSpawners
CALL DurghamSpawnEvent
NoFade
ENDA

DocksSpawners:
LoadCutsceneUnits(DesiderioPilarGroup)
CUSN Desiderio
CUSN Pilar
CHECK_EVENTID WentToHubA6
BNE 1 s0 sC
    AddTrap(RedSigilTrapID, 8, 22) //add the indicator under desiderio
LABEL 1
NoFade
ENDA



DurghamSpawnEvent:
CHECK_EVENTID DurghamRecruitFlag
BNE 776 s0 sC //if durgham recruited, do not spawn him in
    CheckIfHubAChapterCompleted
    BEQ 776 s0 sC //if no chapters completed, do not spawn him in
        LoadPlayerUnits(DurghamUnitGroup) 
LABEL 776
NoFade
ENDA

TavernGreensSpawnEvent:
LoadNPCUnitGroupIfEIDNotSet(HubASamsonArranRecruitFlag, 1, SamsonArranUnitGroup)
LoadNPCUnitGroupIfEIDNotSet(MrYodsenRecruitFlag, 2, MrYodsenUnitGroup)
NoFade
ENDA

PortSpawnEvent:
LoadNPCUnitGroupIfEIDNotSet(WentToHubA2, 3, JaimeUnitGroup, A2Indicator)
LoadNPCUnitGroupIfEIDNotSet(WentToHubA3, 1, OndehBikoUnitGroup, A3Indicator)
LoadNPCUnitGroupIfEIDNotSet(WentToHubA6, 2, RochefortUnitGroup, A6Indicator)
NoFade
ENDA

PortSpawnEventFirstTime:
LoadNPCUnitGroupIfEIDNotSet(WentToHubA2, 3, JaimeUnitGroup, A2Indicator)
LoadNPCUnitGroupIfEIDNotSet(WentToHubA3, 1, OndehBikoUnitGroup, A3Indicator)
LoadNPCUnitGroupIfEIDNotSet(WentToHubA6, 2, RochefortUnitGroup, A6Indicator)
RemoveTrapAtCoords(11,20)
RemoveTrapAtCoords(23,19)
RemoveTrapAtCoords(20,14)
NoFade
ENDA

A2Indicator:
AddTrap(RedSigilTrapID, 11, 20)
NoFade
ENDA

A3Indicator:
AddTrap(RedSigilTrapID, 23, 19)
NoFade
ENDA

A6Indicator:
AddTrap(RedSigilTrapID, 20, 14)
NoFade
ENDA

AddHubATraps:
AddTrap(BlueSigilTrapID,0, 11)
AddTrap(BlueSigilTrapID,0, 12)
AddTrap(BlueSigilTrapID,0, 13)
AddTrap(BlueSigilTrapID,20, 4)
AddTrap(BlueSigilTrapID,21, 4)
AddTrap(BlueSigilTrapID,29, 15)
AddTrap(BlueSigilTrapID,29, 16)
AddTrap(BlueSigilTrapID,8, 20)
AddTrap(RedSigilTrapID,12, 5)
AddTrap(RedSigilTrapID,18, 9)
NoFade
ENDA

ClearHubATraps:
RemoveTrapAtCoords(0, 11)
RemoveTrapAtCoords(0, 12)
RemoveTrapAtCoords(0, 13)
RemoveTrapAtCoords(20, 4)
RemoveTrapAtCoords(21, 4)
RemoveTrapAtCoords(29, 15)
RemoveTrapAtCoords(29, 16)
RemoveTrapAtCoords(8, 20)

RemoveTrapAtCoords(20,4)
RemoveTrapAtCoords(21,4)
RemoveTrapAtCoords(12, 5)
RemoveTrapAtCoords(18, 9)
RemoveTrapAtCoords(11, 20)
RemoveTrapAtCoords(23, 19)
RemoveTrapAtCoords(20, 14)
NoFade
ENDA

AddTavernTraps:
AddTrap(BlueSigilTrapID, 8, 20)
NoFade
ENDA

ClearTavernTraps:
RemoveTrapAtCoords(8, 20)
NoFade
ENDA

AddDockTraps:
AddTrap(BlueSigilTrapID,0,13)
AddTrap(BlueSigilTrapID,0,14)
NoFade
ENDA

ClearDockTraps:
RemoveTrapAtCoords(0, 13)
RemoveTrapAtCoords(0, 14)
RemoveTrapAtCoords(0, 14)
RemoveTrapAtCoords(8, 22) //remove red tile if it's there
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN